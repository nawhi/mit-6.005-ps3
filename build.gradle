apply plugin: 'java'

sourceSets {
    main.java.srcDirs = ['src']
    test.java.srcDirs = ['test']
}

repositories {
    mavenCentral()
}

test {
    testLogging {
        events "PASSED", "STARTED", "FAILED", "SKIPPED", "standardOut", "standardError"
        showStandardStreams = true
    }
}

dependencies {
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    compile group: 'org.antlr', name: 'antlr4-runtime', version: '4.5.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.11.1'
    testCompile group: 'pl.pragmatists', name: 'JUnitParams', version: '1.1.0'
}

task compileAntlr(type: JavaExec) {
    main = 'org.antlr.v4.Tool'
    classpath = files('lib/antlr.jar')
    
    def parserDir = 'src/expressivo/parser'
    def grammar = parserDir + '/Expression.g4'
    
    args = [grammar]
    inputs.files grammar
    outputs.files fileTree(dir: parserDir, excludes: [grammar])
}

compileJava.dependsOn compileAntlr
